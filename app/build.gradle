apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

ext {
    buildNumber = (Long) (System.currentTimeMillis() / 1000L)
    defaultVersionName = '1.1.0'
}

android {
    ndkVersion "23.2.8568313"

    compileSdk 33

    defaultConfig {
        minSdk 19
        targetSdk 33

        versionCode defaultVersionName.replace('.','').toInteger()
        versionName "$defaultVersionName build: $buildNumber"

        // Generate native debug symbols to allow Google Play to symbolicate our native crashes
        ndk.debugSymbolLevel = 'FULL'
    }

    flavorDimensions.add("root")

    productFlavors {
        root {
            // Android O has native mouse capture, so don't show the rooted
            // version to devices running O on the Play Store.
            maxSdk 25

            externalNativeBuild {
                ndkBuild {
                    arguments "PRODUCT_FLAVOR=root"
                }
            }

            applicationId "in.oneplay.root"
            dimension "root"
            archivesBaseName = "$applicationId.$project.name-$defaultVersionName"
            buildConfigField "boolean", "ROOT_BUILD", "true"
        }

        nonRoot {
            externalNativeBuild {
                ndkBuild {
                    arguments "PRODUCT_FLAVOR=nonRoot"
                }
            }

            applicationId "in.oneplay"
            dimension "root"
            archivesBaseName = "$applicationId.$project.name-$defaultVersionName"
            buildConfigField "boolean", "ROOT_BUILD", "false"
        }
    }

    lint {
        disable 'MissingTranslation'
        lintConfig file('lint.xml')
    }

    bundle {
        language {
            // Avoid splitting by language, since we allow users
            // to manually switch language in settings.
            enableSplit = false
        }
        density {
            // FIXME: This should not be necessary but we get
            // weird crashes due to missing drawable resources
            // when this split is enabled.
            enableSplit = false
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_label", "Oneplay (Debug)"
            resValue "string", "app_label_root", "Oneplay (Root Debug)"

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def apiAddress = "https://svrdev.oneplay.in"
            def apiVersion =  "v0"

            buildConfigField "String", "ONEPLAY_SERVER_DEFAULT_IP_ADDRESS", "\"43.239.110.82\""
            buildConfigField "String", "ONEPLAY_APP_WELCOME_LINK_PATH", "\"\""
            buildConfigField "String", "ONEPLAY_APP_WELCOME_LINK_QUERY", "\"\""
            buildConfigField "String", "ONEPLAY_API_START_VM_ENDPOINT", "\"${apiAddress}/${apiVersion}/frontend/start_game?vm_ip=\""
            buildConfigField "String", "ONEPLAY_API_GET_SESSION_ENDPOINT", "\"${apiAddress}/${apiVersion}/client/get_session/\""
            buildConfigField "String", "ONEPLAY_API_EVENTS_ENDPOINT", "\"\""
        }
        release {
            applicationIdSuffix ""
            resValue "string", "app_label", "Oneplay"
            resValue "string", "app_label_root", "Oneplay (Root)"

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def apiAddress = "https://client-apis.oneplay.in"
            def apiVersion = "v1"

            buildConfigField "String", "ONEPLAY_SERVER_DEFAULT_IP_ADDRESS", "\"\""
            buildConfigField "String", "ONEPLAY_APP_WELCOME_LINK_PATH", "\"/dashboard\""
            buildConfigField "String", "ONEPLAY_APP_WELCOME_LINK_QUERY", "\"\""
            buildConfigField "String", "ONEPLAY_API_START_VM_ENDPOINT", "\"\""
            buildConfigField "String", "ONEPLAY_API_GET_SESSION_ENDPOINT", "\"${apiAddress}/client/${apiVersion}/get_session/\""
            buildConfigField "String", "ONEPLAY_API_EVENTS_ENDPOINT", "\"${apiAddress}/backend/events/register\""

            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }
}

dependencies {
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    implementation 'org.jcodec:jcodec:0.2.3'
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'
    implementation 'com.squareup.okio:okio:1.17.5'
    implementation 'org.jmdns:jmdns:3.5.7'
    implementation 'com.github.cgutman:ShieldControllerExtensions:1.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.13'
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation platform('com.google.firebase:firebase-bom:29.3.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'
    implementation 'com.google.android.play:app-update:2.0.0'
}
